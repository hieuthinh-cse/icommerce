plugins {
    id 'idea'
    id 'org.springframework.boot' version '2.2.5.RELEASE' apply false
    id 'io.freefair.lombok' version '5.0.0-rc4' apply false
    id 'org.unbroken-dome.test-sets' version '2.2.1' apply false
    id "com.gorylenko.gradle-git-properties" version "2.2.2" apply false
}

allprojects {
    group = 'vn.icommerce'
    version = '1.0.0'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.unbroken-dome.test-sets'
    apply plugin: 'jacoco'
    apply plugin: 'com.gorylenko.gradle-git-properties'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    configurations {
        all*.exclude module: 'spring-boot-starter-logging'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    gitProperties {
        dateFormat = "yyyy-MM-dd'T'HH:mmZ"
        dateFormatTimeZone = "GMT+7"
        keys = ['git.branch','git.commit.id', 'git.commit.id.abbrev', 'git.commit.user.email', 'git.build.user.email','git.commit.time','git.tags']
    }

    tasks.withType(Test) {
        systemProperty 'file.encoding', 'UTF-8'
    }

    testSets {
        integrationTest
    }

    integrationTest {
        systemProperties project.properties.subMap(['it.pipelineEnv'])
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    repositories {
        jcenter()
    }

    dependencies {

        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation 'de.codecentric:spring-boot-admin-starter-client:2.2.2'
        implementation 'org.springframework.cloud:spring-cloud-starter-sleuth:2.2.2.RELEASE'
        implementation 'org.postgresql:postgresql:42.2.10'
        implementation 'org.projectlombok:lombok:1.18.12'
        implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.4.2'
        implementation 'org.elasticsearch.client:elasticsearch-rest-client:7.4.2'
        implementation 'org.elasticsearch:elasticsearch:7.4.2'
        runtimeOnly 'com.lmax:disruptor:3.4.2'

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    }
}

apply from: 'jacocoTestCoverage.gradle'
